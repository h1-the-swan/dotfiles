# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files


# Setting PATH for Python 2.7
PATH="/Library/Frameworks/Python.framework/Versions/2.7/bin:${PATH}"
export PATH

# Setting PATH for TeX
PATH="/usr/texbin:${PATH}"
export PATH

# add my modules to python path
PYTHONPATH=$HOME/.python_modules/:$PYTHONPATH
export PYTHONPATH

if which pyenv > /dev/null; then eval "$(pyenv init -)"; fi

# Linux-gnu specific settings
if [[ "$OSTYPE" == "linux-gnu" ]]; then
	# colors
	# http://superuser.com/questions/370556/vim-colors-not-working-properly-in-terminal
	TERM=xterm-256color
	export TERM
	
	# Setting PATH for Anaconda
	PATH=$HOME/anaconda2/bin:$PATH
	export PATH
fi

# load config file with environment variables
CONFIG_VARS_FILE=~/.config_vars.zsh
if [[ -f $CONFIG_VARS_FILE ]]; then
	source $CONFIG_VARS_FILE
fi
